import groovy.xml.XmlParser
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    ext {
        compileSdkVersion   = 34
        targetSdkVersion    = 34
        appCompatVersion    = "1.7.0"
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    afterEvaluate {
        // check if `android` block is available and namespace isn't set
        if(it.hasProperty('android') && it.android.namespace == null){
            def manifest = new XmlParser().parse(file(it.android.sourceSets.main.manifest.srcFile))
            android.namespace= manifest.@package
        }
        tasks.withType(KotlinCompile).tap {
            configureEach {
                if (project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) {
                    kotlinOptions.jvmTarget = android.compileOptions.sourceCompatibility
                } else {
                    kotlinOptions.jvmTarget = sourceCompatibility
                }
            }
        }
    }
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}


configurations.all {
    resolutionStrategy {
        force 'androidx.core:core-ktx:1.6.0'
    }
}